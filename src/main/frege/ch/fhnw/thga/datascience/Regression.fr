module ch.fhnw.thga.datascience.Regression where

average :: [Double] -> Double
average [] = error "no average for empty lists"
average xs = sum xs / fromIntegral (length xs)

linearRegression :: [(Double,Double)] -> (Double,Double)
linearRegression []  = error "insufficient data for linearRegression: no data points"
linearRegression [x] = error "insufficient data for linearRegression: only one data point"
linearRegression xys = (a, b)  where
    b = sumOfMultipliedXYDiffs / sumOfSquaredXDiffs
    sumOfMultipliedXYDiffs = sum $ map (\(x,y) -> (x-avgX)*(y-avgY)) xys
    sumOfSquaredXDiffs     = sum $ map (\(x,y) -> (x-avgX)*(x-avgX)) xys
    avgX = average $ map (\(x,y) -> x) xys
    avgY = average $ map (\(x,y) -> y) xys
    a = avgY - b * avgX


import Test.QuickCheck
epsilon = 0.001
closeEnough (a1,b1) (a2,b2) = abs ( a1 - a2 ) < epsilon && abs ( b1 - b2 ) < epsilon

-- if all data points are on the same line, the coordinates of that line must be the regression
line_property = property $ \(a::Double, b::Double) ->
    closeEnough (a,b) $ linearRegression [ (x, a + b * x) | x <- [-2.0, -1.0, 0.0, 1.0, 2.0] ]

-- if all function values are equal, then the rise is 0 and a is the function value
horizontal_property = property $ \(a::Double) ->
    closeEnough (a, 0.0) $ linearRegression [ (x, a) | x <- [-2.0, -1.0, 0.0, 1.0, 2.0] ]

weightHeight = -- from https://online.stat.psu.edu/stat462/node/92/
    [(63.0, 127.0) ,(64.0, 121.0) ,(66.0, 142.0) ,(69.0, 157.0) ,(69.0, 162.0)
    ,(71.0, 156.0) ,(71.0, 169.0) ,(72.0, 165.0) ,(73.0, 181.0) ,(75.0, 208.0)
    ]
sample_property = once $ closeEnough (-266.5343, 6.1375) $ linearRegression weightHeight

-- if the values are on a vertical line, regression does not compute (could be fixed)
vertical_property = once $ (fst result).isNaN && (snd result).isNaN where
    result = linearRegression [(0.0,0.0), (0.0,1.0)]

main = do
    quickCheck line_property
    quickCheck horizontal_property
    quickCheck sample_property
    quickCheck vertical_property
